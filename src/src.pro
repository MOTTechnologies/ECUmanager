# File generated by kdevelop's qmake manager.
# -------------------------------------------
# Subdir relative project main directory: ./src
# Target is an application: ../bin/ecumanager
PROJECT = ecumanager
TEMPLATE = app
DEPENDPATH += .
OBJECTS_DIR             = tmp
MOC_DIR                 = tmp
HEADERS += qt4application.h \
	renderarea.h \
    plotter.h \
    2Dgraph.h \
    firm_updater.h \
    QViewer.h \
    QSubViewer.h \
    QMap3D.h \
    QuickTune.h \
    QPlayer.h \
    QInjectorTest.h \
    commthread.h \
    commCore_FreeEMS.h \
	FreeEMS.h
SOURCES += qt4application.cpp \
    main.cpp \
    RenderArea.cpp \
    plotter.cpp \
    2Dgraph.cpp \
    firm_updater.cpp \
    QViewer.cpp \
    QSubViewer.cpp \
    qt4applicationEXTRAS.cpp \
    QMap3D.cpp \
    QuickTune.cpp \
    QPlayer.cpp \
    QInjectorTest.cpp \
    qt4applicationCREATETHINGS.cpp \
    commthread.cpp \
	commCore_FreeEMS.cpp

INCLUDEPATH += ../lib/QExtSerialPort \
				/usr/include/qwtplot3d-qt4
QMAKE_LIBDIR += ../lib/QExtSerialPort/build \
				/usr/lib/
RESOURCES = application.qrc
TARGET = ecumanager
DESTDIR = ../bin/
CONFIG += warn_on \
    thread \
    qt
unix { 
    CONFIG += debug
    
    # Prefix: base instalation directory
    isEmpty( PREFIX ):PREFIX = /usr/
    DEB_BUILD = $$system(echo \$DEB_BUILD_OPTIONS)
    contains(DEB_BUILD, nostrip):QMAKE_STRIP = :
    DEFINES += PREFIX=\\\"$${PREFIX}\\\"
    target.path = $${PREFIX}/bin
    INSTALLS = target
    
	# Shared library installation
	SHLIBS_PATH = $${PREFIX}/lib
	shlibs_path.path = $${SHLIBS_PATH}
	shlibs_path.files =	libqextserialport.so \
						libqextserialport.so.1 \
						libqextserialport.so.1.2 \
						libqextserialport.so.1.2.0

    # Menu entry
    MENU_ENTRY_PATH = $${PREFIX}/share/applications
    menu_entry.path = $${MENU_ENTRY_PATH}
    menu_entry.files = ecumanager.desktop
    INSTALLS += menu_entry
    
    # ECUmanager Icon
    ICON_PATH = $${PREFIX}/share/pixmaps
    ecum_icon.path = $${ICON_PATH}
	ecum_icon.files = icons/ecumanager-icon.png
    INSTALLS += ecum_icon
}
LIBS += -lqextserialport \
		-lqwtplot3d-qt4
QT += opengl
